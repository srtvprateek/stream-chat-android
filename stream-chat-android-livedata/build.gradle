import com.getstream.sdk.chat.Dependencies

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'com.github.dcendents.android-maven'
    id 'org.jlleitschuh.gradle.ktlint'
}

ext {
    PUBLISH_GROUP_ID = com.getstream.sdk.chat.Configuration.artifactGroup
    PUBLISH_VERSION = com.getstream.sdk.chat.Configuration.versionName
    PUBLISH_ARTIFACT_ID = 'stream-chat-android-livedata'
}

apply from: "${rootDir}/scripts/publish-module.gradle"
apply from: "${rootDir}/scripts/android.gradle"

android {
    defaultConfig {
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    testOptions.unitTests {
        includeAndroidResources = true
        returnDefaultValues = true
        all {
            testLogging {
                events 'failed', 'standardOut', 'standardError'
                showExceptions true
                showCauses true
                showStackTraces true
                exceptionFormat 'full'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }
}

kapt {
    arguments {
        arg("room.incremental", "true")
    }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions {
        freeCompilerArgs += [
                '-progressive',
                '-Xexplicit-api=strict',
                '-Xopt-in=io.getstream.chat.android.core.internal.InternalStreamChatApi',
        ]
    }
}

dependencies {
    implementation Dependencies.kotlinStdLib

    implementation Dependencies.androidxAppCompat
    implementation Dependencies.androidxCoreKtx

    implementation project(":stream-chat-android-offline")

    testImplementation project(":stream-chat-android-test")
}
